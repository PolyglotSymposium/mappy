if = \cond (then) (else) -> [[
  default-take [take :truthy cond] (:false else) then
]]

true = (:truthy :true)
false = (:truthy :false)

equal? = \a b -> [default-take a (b true) false]

not = \v -> [if v [give :truthy :false v] [give :truthy :true v]]

empty? = \map -> [default-take map (() true) false]

zero = ()

succ = \num -> [give :pred num ()]

pred = \num -> [take :pred num]

add = \a b -> [
  if [empty? a]
    b
    [succ [add [pred a] b]]
]

two = [succ [succ zero]]

identity = \a -> a

nil = ()

cons = \value list -> (:head value :tail list)

apply = \fn v -> [fn v]

assert = \c a b -> [if c :pass :fail]

main =
  (:test-equal-for-keywords [assert [equal? :foo :foo]]
   :test-equal-for-equal-maps [assert [equal? two two]]
   :test-equal-for-non-equal-maps [assert [not [equal? zero two]]]
   :test-equal-for-functions [assert [equal? apply apply]]
   :test-cons-works [assert [equal? [cons :foo nil] (:head :foo :tail ())]]
   :test-add-works-for-trivial-case [assert [equal? [add zero two] two]]
   :test-add-works-for-harder-case [assert [equal? [add two two] [succ [succ two]]]]
   :test-first-class-functions [assert [equal? [apply identity :foo] :foo]]
)
